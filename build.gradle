plugins {
    // Plugin that applies Google-Java-format to the Java files in the project.
    // https://github.com/sherter/google-java-format-gradle-plugin
    id 'com.github.sherter.google-java-format' version '0.8'
}

apply plugin: 'java'

ext {
    jsr308 = System.getenv('JSR308') ?: file(new File("..")).absolutePath
    cfPath = "${jsr308}/checker-framework"
    cfiPath = "${jsr308}/checker-framework-inference"
    afu = "${jsr308}/annotation-tools/annotation-file-utilities"
    ontologyPath = "${jsr308}/ontology"
}

println '==================================='
println '         Ontology Checker          '
println '==================================='
println ''
println '-------------------------------'
println 'Important Environment Variables'
println '-------------------------------'
println 'JSR308: ' + jsr308
println 'CF:     ' + cfPath
println 'CFI:    ' + cfiPath

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile fileTree(dir: "${cfPath}/checker/dist", include: "checker.jar")
    compile fileTree(dir: "${cfiPath}/dist", include: "checker-framework-inference.jar")
    // sat4j solver dependency
    compile 'org.ow2.sat4j:org.ow2.sat4j.core:2.3.6'
    compile 'org.ow2.sat4j:org.ow2.sat4j.maxsat:2.3.6'
    // z3 solver dependency
    compile fileTree(dir: "${cfiPath}/lib", include: "com.microsoft.z3.jar")

    compile fileTree(dir: "${cfiPath}/dist", include: "inference-framework-test-lib.jar")

    // CF test lib dependency
    testCompile fileTree(dir: "${cfPath}/framework-test/build/libs", include: "framework-test-*.jar")
    testCompile 'junit:junit:4.13.1'
}

sourceSets {
    main {
        java {
            srcDirs = ["src"]
        }

        resources {
            srcDirs = ["src"]
            include "**/*.astub"
        }
    }

    test {
        java {
            srcDirs = ["tests"]
        }
    }
}

compileJava {
    options.compilerArgs = [
            '-implicit:class',
            '-Awarns',
            '-Xmaxwarns', '10000',
    ]
}

afterEvaluate {
    // Create a task for each JUnit test class whose name is the same as the JUnit class name.
    sourceSets.test.allJava.filter { it.path.contains("${ontologyPath}/tests") }.forEach { file ->
        String junitClassName = file.name.replaceAll(".java", "")
        String testName = junitClassName.replaceAll("Test", "")
        tasks.create(name: "${junitClassName}", type: Test) {
            description "Run ${testName} tests."
            include "**/${name}.class"
        }
    }

    // Configure JUnit tests
    tasks.withType(Test) {
        group 'Verification'

        systemProperties = [
                'path.afu.scripts': "${afu}/scripts",
                'path.inference.script': "${cfiPath}/scripts/inference"
        ]

        if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
            systemProperties += [JDK_JAR: "${cfPath}/checker/dist/jdk8.jar"]
        }

        environment "external_checker_classpath", "${ontologyPath}/build/classes/java/main:${ontologyPath}/build/libs/ontology.jar"


        if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
            jvmArgs "-Xbootclasspath/p:${cfiPath}/dist/javac.jar"
        }

        testLogging {
            // Always run the tests
            outputs.upToDateWhen { false }
            // The following prints out each time a test is passed.
            events "passed", "skipped", "failed", "standardOut", "standardError"

            // Show the found unexpected diagnostics and expected diagnostics not found.
            exceptionFormat "full"
            showExceptions true
            showCauses true
            showStackTraces true
            showStandardStreams true
        }

        // After each test, print a summary.
        afterSuite { desc, result ->
            if (desc.getClassName() != null) {
                long mils = result.getEndTime() - result.getStartTime()
                double seconds = mils / 1000.0

                println "Testsuite: ${desc.getClassName()}\n" +
                        "Tests run: ${result.testCount}, " +
                        "Failures: ${result.failedTestCount}, " +
                        "Skipped: ${result.skippedTestCount}, " +
                        "Time elapsed: ${seconds} sec\n"
            }
        }
    }
}

tasks.clean {
    delete += "build"
    delete += "dist"
    delete += "testdata"
}

/* Configuration for formatting */
googleJavaFormat {
    // toolVersion '1.3'
    options style: 'AOSP'
}
tasks.googleJavaFormat {
    group 'Formatting'
    description = "Reformat Java source code with Google-Java-format"
    exclude 'testing'
    exclude 'testdata'
    exclude 'annotated'
    exclude 'worked-benchmarks'
}
tasks.verifyGoogleJavaFormat {
    group 'Formatting'
    description = "Check Java source code is in Google-Java-format"
    exclude 'testing'
    exclude 'testdata'
    exclude 'annotated'
    exclude 'worked-benchmarks'
}
